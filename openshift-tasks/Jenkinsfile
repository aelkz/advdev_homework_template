#!groovy
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-agent-appdev",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "2Gi",
      resourceRequestCpu: "1",
      resourceLimitCpu: "2"
    )
  ]
) {

  /*
    MitziCom has a shared Nexus Artifact Repository deployed in the cluster.
    - The Service for Nexus is nexus3.gpte-hw-cicd.svc.cluster.local:8081/repository/maven-all-public
    - The public route for Nexus is http://nexus3-gpte-hw-cicd.apps.na311.openshift.opentlc.com
    - The Service for the Nexus Container Registry is https://nexus3-registry.gpte-hw-cicd.svc.cluster.local:5000.
    - The public route for the Nexus Container Registry is https://nexus3-registry-gpte-hw-cicd.apps.na311.openshift.opentlc.com
    - The SonarQube service is sonarqube.gpte-hw-cicd.svc.cluster.local:9000.
    - The SonarQube public route is http://sonarqube-gpte-hw-cicd.apps.na311.openshift.opentlc.com
  */

  def userGUID = "fee6"
  def userGIT = "aelkz"
  def nexusService = "http://nexus3.gpte-hw-cicd.svc.cluster.local:8081"
  def nexusRoute = "http://nexus3-gpte-hw-cicd.apps.na311.openshift.opentlc.com"
  def nexusContainerRegistryService = "https://nexus3-registry.gpte-hw-cicd.svc.cluster.local:5000"
  def nexusContainerRegistryRoute = "https://nexus3-registry-gpte-hw-cicd.apps.na311.openshift.opentlc.com"
  def nexusContainerRegistryDockerRoute = "docker://nexus3-registry.gpte-hw-cicd.svc.cluster.local:5000"
  def sonarQubeService = "sonarqube.gpte-hw-cicd.svc.cluster.local:9000"
  def sonarQubeRoute = "http://sonarqube-gpte-hw-cicd.apps.na311.openshift.opentlc.com"
  def gitRepo = "https://github.com/" + userGIT + "/advdev_homework_template.git"

  node('skopeo-pod') {
    // Define Maven Command to point to the correct
    // settings for our Nexus installation
    def mvnCmd = "mvn -s ../nexus_settings.xml"

    // Checkout Source Code.
    stage('Checkout Source') {
      // git url: "${gitRepo}"
      checkout scm
    }

    // Build the Tasks Service
    dir('openshift-tasks') {
      /* The following variables need to be defined at the top level
         and not inside the scope of a stage - otherwise they would not
         be accessible from other stages.
         Extract version from the pom.xml
      */
      def version = getVersionFromPom("pom.xml")

      // TBD Set the tag for the development image: version + build number
      def devTag  = "${version}-${currentBuild.number}"
      // Set the tag for the production image: version
      def prodTag = "${version}"
      def devProject  = "${GUID}-tasks-dev"
      def prodProject = "${GUID}-tasks-prod"

      // Using Maven build the war file
      // Do not run tests in this step
      stage('Build war') {
        echo "Building version ${devTag}"

        // TBD: Execute Maven Build
        sh "${mvnCmd} clean package -DskipTests=true"
      }

      // TBD: The next two stages should run in parallel
      // Using Maven run the unit tests
      stage('Parallel Test and Analysis') {
        parallel 'Unit Tests': {
            // Using Maven run the unit tests
            echo "Running Unit Tests"

            // Execute Unit Tests
            sh "${mvnCmd} test"

            // This next step is optional.
            // It displays the results of tests in the Jenkins Task Overview
            step([$class: 'JUnitResultArchiver', testResults: '**/target/surefire-reports/TEST-*.xml'])
          }, 'Code Analysis': {
            // Using Maven to call SonarQube for Code Analysis
            echo "Running Code Analysis"

            // Execute Sonarqube Tests
            sh "${mvnCmd} sonar:sonar -Dsonar.host.url=${sonarQubeRoute} -Dsonar.projectName=${JOB_BASE_NAME} -Dsonar.projectVersion=${devTag}"
          }
        failFast: true
      }

      // Publish the built war file to Nexus
      stage('Publish to Nexus') {
        echo "Publish to Nexus"

        // TBD: Publish to Nexus
        sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::${nexusService}/repository/releases"
      }

      // Build the OpenShift Image in OpenShift and tag it.
      stage('Build and Tag OpenShift Image') {
        echo "Building OpenShift container image tasks:${devTag}"

        // TBD: Build Image, tag Image
        script {
          openshift.withCluster() {
            openshift.withProject("${devProject}") {
              openshift.selector("bc", "tasks").startBuild("--from-file=${nexusService}/repository/releases/org/jboss/quickstarts/eap/tasks/${version}/tasks-${version}.war", "--wait=true")
              openshift.tag("tasks:latest", "tasks:${devTag}")
            }
          }
        }
      }

      // Deploy the built image to the Development Environment.
      stage('Deploy to Dev') {
        echo "Deploying container image to Development Project"

        // TBD: Deploy to development Project
        //      Set Image, Set VERSION
        //      Make sure the application is running and ready before proceeding

        script {
          // Update the Image on the Development Deployment Config
          openshift.withCluster() {
            openshift.withProject("${devProject}") {
              // OpenShift 3.11
              openshift.set("image", "dc/tasks", "tasks=docker-registry.default.svc:5000/${devProject}/tasks:${devTag}")

              // OpenShift 4
              // openshift.set("image", "dc/tasks", "tasks=image-registry.openshift-image-registry.svc:5000/${devProject}/tasks:${devTag}")

              // Update the Config Map which contains the users for the Tasks application
              // (just in case the properties files changed in the latest commit)
              openshift.selector('configmap', 'tasks-config').delete()
              def configmap = openshift.create('configmap', 'tasks-config', '--from-file=./configuration/application-users.properties', '--from-file=./configuration/application-roles.properties')

              // Set environment variable version
              sh "oc set env dc/tasks VERSION='${devTag} (tasks-dev)' -n ${devProject}"

              // Deploy the development application.
              openshift.selector("dc", "tasks").rollout().latest();

              // Wait for application to be deployed
              def dc = openshift.selector("dc", "tasks").object()
              def dc_version = dc.status.latestVersion
              def rc = openshift.selector("rc", "tasks-${dc_version}").object()

              echo "Waiting for ReplicationController tasks-${dc_version} to be ready"
              while (rc.spec.replicas != rc.status.readyReplicas) {
                sleep 7
                rc = openshift.selector("rc", "tasks-${dc_version}").object()
              }
              echo "ReplicationControllers for tasks-${dc_version} are ready"
            }
          }
        }

      }

      // Copy Image to Nexus container registry
      stage('Copy Image to Nexus container registry') {
        echo "Copy image to Nexus container registry"

        // TBD: Copy image to Nexus container registry
        // TBD: Tag the built image with the production tag.

        script {
          sh "skopeo copy --src-tls-verify=false \
              --dest-tls-verify=false \
              --src-creds openshift:\$(oc whoami -t) \
              --dest-creds admin:redhat \
              docker://docker-registry.default.svc.cluster.local:5000/${devProject}/tasks:${devTag} \
              docker://nexus-registry.gpte-hw-cicd.svc.cluster.local:5000/tasks:${devTag}"

          // Tag the built image with the production tag.
          openshift.withCluster() {
            openshift.withProject("${prodProject}") {
              openshift.tag("${devProject}/tasks:${devTag}", "${devProject}/tasks:${prodTag}")
            }
          }
        }
      }

      // Blue/Green Deployment into Production
      // -------------------------------------
      def currentApp = ""
      def destApp = "tasks-green"

      stage('Blue/Green Production Deployment') {
        // TBD: Determine which application is active
        //      Set Image, Set VERSION
        //      Deploy into the other application
        //      Make sure the application is running and ready before proceeding

        echo "Blue/Green Deployment"
        script {
          openshift.withCluster() {
            openshift.withProject("${prodProject}") {
              currentApp = openshift.selector("route", "tasks").object().spec.to.name
              if (currentApp == "tasks-green") {
                destApp = "tasks-blue"
              }
              echo "Active Application:      " + currentApp
              echo "Destination Application: " + destApp

              // Update the Image on the Production Deployment Config
              def dc = openshift.selector("dc/${destApp}").object()

              // OpenShift 4
              // dc.spec.template.spec.containers[0].image="image-registry.openshift-image-registry.svc:5000/${devProject}/tasks:${prodTag}"

              // OpenShift 3.11
              dc.spec.template.spec.containers[0].image="docker-registry.default.svc:5000/${devProject}/tasks:${prodTag}"

              openshift.apply(dc)

              // Update Config Map in change config files changed in the source
              openshift.selector("configmap", "${destApp}-config").delete()
              def configmap = openshift.create("configmap", "${destApp}-config", "--from-file=./configuration/application-users.properties", "--from-file=./configuration/application-roles.properties" )

              sh "oc set env dc/${destApp} VERSION='${prodTag} ${destApp}' -n ${prodProject}"

              // Deploy the inactive application.
              openshift.selector("dc", "${destApp}").rollout().latest();

              // Wait for application to be deployed
              def dc_prod = openshift.selector("dc", "${destApp}").object()
              def dc_version = dc_prod.status.latestVersion
              def rc_prod = openshift.selector("rc", "${destApp}-${dc_version}").object()
              echo "Waiting for ${destApp} to be ready"
              while (rc_prod.spec.replicas != rc_prod.status.readyReplicas) {
                sleep 7
                rc_prod = openshift.selector("rc", "${destApp}-${dc_version}").object()
              }
            }
          }
        }
      }

      stage('Switch over to new Version') {
        echo "Switching Production application to ${destApp}."
        // TBD: Execute switch

        script {
          openshift.withCluster() {
            openshift.withProject("${prodProject}") {
              def route = openshift.selector("route/tasks").object()
              route.spec.to.name="${destApp}"
              openshift.apply(route)

            }
          }
        }
      }
    }
  }
}

// Convenience Functions to read version from the pom.xml
// Do not change anything below this line.
// --------------------------------------------------------
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}
